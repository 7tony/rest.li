/*
   Copyright (c) 2012 LinkedIn Corp.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

final Configuration generatorConfig = project(':generator').configurations.default

project.sourceSets.all { SourceSet sourceSet ->
  final String inputDataSchemaParentDirPath = "src${File.separatorChar}${sourceSet.name}"
  final String pegasusDirName = 'pegasus'
  final String inputDataSchemaDirPath = "${inputDataSchemaParentDirPath}${File.separatorChar}${pegasusDirName}"
  final FileTree inputDataSchemaFiles = project.fileTree(dir: inputDataSchemaDirPath, includes: ["**${File.separatorChar}*.pdsc".toString()])
  if (inputDataSchemaFiles.empty)
  {
    return;
  }

  final String outputDirPath = rootProject.ext.build.getDataTemplateOutDirPath(project, sourceSet)
  sourceSet.java.srcDir(outputDirPath)

  rootProject.ext.build.dataTemplateTasks[sourceSet] = project.task(sourceSet.name + 'GenerateDataTemplate', type: JavaExec) {
    main = 'com.linkedin.pegasus.generator.PegasusDataTemplateGenerator'
    classpath = generatorConfig + generatorConfig.allArtifacts.files
    args outputDirPath
    args inputDataSchemaFiles
    systemProperties(['generator.resolver.path': inputDataSchemaDirPath])
    doFirst {
      project.mkdir(outputDirPath)
    }
  }

  final Task jarTask = project.tasks[sourceSet.getTaskName('', 'jar')]
  jarTask.from(inputDataSchemaParentDirPath) {
    include "${pegasusDirName}${File.separatorChar}**${File.separatorChar}*.pdsc".toString()
  }

  project.tasks[sourceSet.compileJavaTaskName].dependsOn(rootProject.ext.build.dataTemplateTasks[sourceSet])
}
